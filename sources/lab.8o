: audio_lab_bull
	0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA 0xAA
: audio_lab_cow
	0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0xFF 0x00 0x00 0x00 0x00 0x00 0x00 0x00 0x00

: lab_pin
 -1 -1 -1 -1

: lab_stats
: lab_stats_bulls
0
: lab_stats_cows
0


# va == key, vd == index
: lab_update_stats
	i := lab_pin
	load v3
	i := lab_stats_bulls
	load v4 - v4
	v5 <<= vd
	v5 <<= v5
	v5 <<= v5
	v5 <<= v5
	i := lab_bull_index
	save v5 - v5

:next lab_bull_index
	if va != v0 then jump lab_update_cows

	v4 += 1
	i := lab_stats_bulls
	save v4 - v4

: lab_update_cows
	i := lab_stats_cows
	load v4 - v4
	if va == v0 then v4 += 1
	if va == v1 then v4 += 1
	if va == v2 then v4 += 1
	if va == v3 then v4 += 1
	i := lab_stats_cows
	save v4 - v4

	return

: lab_enter_pin
	dialog_lab_security_1

	i := lab_stats
	v0 := 0
	save v0
	save v0

	i := lab_pin
	load v3
	if v0 != -1 then jump lab_enter_pin_key

#generating pin first time
	v0 := random 0xf
	if v0 > 9 then v0 += -6
	v1 := random 0xf
	if v1 > 9 then v1 += -6
	v2 := random 0xf
	if v2 > 9 then v2 += -6
	v3 := random 0xf
	if v3 > 9 then v3 += -6

	v0 := 1
	v1 := 2
	v2 := 3
	v3 := 4
	i := lab_pin
	save v3

: lab_enter_pin_key
	vd := 0
	loop
	: lab_enter_key_loop
		va := key
		if va >= 10 then jump lab_enter_key_loop
		lab_update_stats
		audio_play_dtmf

		v0 := vd
		v0 += v0
		v0 += v0

		va := 10
		va += v0
		va += v0

		vb := 40
		vc := 0x2a # '*'
		draw_font_char

		vd += 1
		if vd < 4 then
	again

	va := 10

	i := lab_stats
	load v1
	v1 -= v0 #sub bulls from cows
	i := lab_stats
	save v1

: lab_display_bulls

	i := lab_stats_bulls
	load v0
	if v0 == 0 then jump lab_display_cows
	v0 += -1
	i := lab_stats_bulls
	save v0

	vb := 50
	vc := 0x2b # +
	draw_font_char
	va += 6

	vd := va
	vf := 20
	i := audio_lab_bull
	audio_play_sync_vf
	va := vd

	jump lab_display_bulls

#COWS

: lab_display_cows
	va := vd
	i := lab_stats

: lab_display_cows_loop

	i := lab_stats_cows
	load v0

	if v0 == 0 then jump lab_enter_pin_exit
	v0 += -1
	i := lab_stats_cows
	save v0

	vb := 50
	vc := 0x3f # ?
	draw_font_char
	va += 6

	vd := va
	vf := 20
	i := audio_lab_cow
	audio_play_sync_vf
	va := vd

	jump lab_display_cows_loop

: lab_enter_pin_exit
	return
